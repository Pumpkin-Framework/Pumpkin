plugins {
    id "java"
    id "idea"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group = "nl.jk-5.pumpkin"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        name "sponge"
        url "http://repo.spongepowered.org/maven/"
    }
    maven {
        name "jk-5"
        url "http://maven.jk-5.nl/"
    }
}

configurations {
    shaded
    compile.extendsFrom shaded
}

dependencies {
    compile "org.spongepowered:spongeapi:7.1.0-SNAPSHOT"

    shaded 'org.postgresql:postgresql:9.4-1206-jdbc41'
    shaded 'org.javassist:javassist:3.19.0-GA'
    shaded 'com.j256.ormlite:ormlite-jdbc:4.48'
    shaded 'org.apache.httpcomponents:httpclient:4.5.1'

    shaded 'nl.jk5.pumpkin:jnlua:1.1.0'
    shaded 'net.sf.trove4j:trove4j:3.0.3'
}

// jar {
//     from {
//         configurations.shaded.collect {
//             it.isDirectory() ? it : zipTree(it).matching {
//                 exclude "META-INF", "META-INF/**", "*META-INF*", "meta-inf"
//                 exclude "**.jar", "**/*.jar", "*.jar"
//             }
//         }
//     }
// }

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.manifest {
    attributes "Built-By": System.getProperty("user.name")
    attributes "Build-Jdk": System.getProperty("java.version")
    attributes "Created-By": "Gradle " + gradle.gradleVersion
    attributes "Implementation-Version": project.version
    attributes "Implementation-Title": "nl.jk_5.pumpkin"
    attributes "Implementation-Vendor": "jk-5"
}

shadowJar {
    configurations = [project.configurations.shaded]
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    relocate 'org.apache', 'nl.jk5.pumpkin.shadow.org.apache'
}
